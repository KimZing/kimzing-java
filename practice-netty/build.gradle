plugins {
    id 'java'
    // protobuf编译插件，会添加gradle任务进行编译生成类文件
    id "com.google.protobuf" version "0.9.1"
}

group 'org.kimzing'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.netty:netty-all:4.1.67.Final'
    // protobuf的相关依赖，比如类型声明等
    implementation 'com.google.protobuf:protobuf-java:3.10.0'
    implementation 'com.alibaba:fastjson:2.0.18'
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

// 源文件配置
sourceSets {
    main {
        proto {
            // proto源文件的位置，默认 'src/main/proto' 可以写多个
            srcDir 'src/main/java/com/kimzing/netty/protobuf/proto'
        }
    }
    test {
        proto {
            // 默认 'src/test/proto'
            srcDir 'src/test/protobuf'
        }
    }
}

protobuf {
    protoc {
        // 使用依赖包中的protoc进行编译，m1芯片需特殊指定, 或者在本地安装protoc的编译程序
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.10.1:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.10.1'
        }
    }
    // 生成的类文件的位置 $buildDir:构建目录  $projectDir:项目目录
    generatedFilesBaseDir = "$projectDir/src/generated"
}